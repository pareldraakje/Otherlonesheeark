**
** Desert tree thing.
** Open source, do whatever you want with the code/images. No need to credit.
** Pilla
**

*
* CAOS2PRAY CHUNK
*# Pray-File "ols_jungletree.agents"
*# DS-Name "OLS Jungle Tree"
*# attach ols_jungletree.c16
*# desc = "A tree that spawns nuts."
*# Agent Animation File = "ols_jungletree.c16"
*# Agent Sprite First Image = 0
*# Agent Animation Gallery = "ols_jungletree"
*
*
* 36450 = custom species number
* Objects:
* 2 4 36450 - Tree
* 2 7 36450 - Flower
* 2 3 36450 - Seed
* Scripts:
* 2 4 36450 1 - Tree dispenser
* 2 4 36450 9 - Tree timer
* 2 7 36450 9 - Flower timer
* 2 3 36450 9 - Seed timer
* 2 3 36450 12 - Seed eat
*
*
*
*

** Create the tree (attr: activateable 4 + invisible 16 + suffer collisions 64 + suffer physics 128 = 212)
new: simp 2 4 36450 "ols_jungletree" 1 0 0
attr 212
bhvr 0
perm 100
accg 30
elas 0
fric 100
mvsf 32480 13350
tick 400

** The tree timer script
scrp 2 4 36450 9
	setv va00 posl
	setv va01 post
* Create a flower (attr: invisible 16)
	new: simp 2 7 36450 "ols_jungletree" 1 1 rand 410 420
	attr 16
	bhvr 0
	tick rand 600 900
	addv va00 rand 10 295
	addv va01 rand 58 118
	mvsf va00 va01
endm

* The tree dispenser script
scrp 2 4 36450 1
	setv va00 posl
	setv va01 post
* Create seed (attr: mouseable 2 + collisions 64 + physics 128 = 194, bhvr: eat 16 + pickup 32 = 48)
	new: simp 2 3 36450 "ols_jungletree" 1 2 rand 400 600
	attr 194
	bhvr 48
	tick 1000
	perm 100
	accg 1.2
	elas 30
	fric 60
	aero 1.1
	emit 7 1
	addv va00 rand 10 295
	addv va01 rand 58 118
	mvsf va00 va01
endm

* flower script: turn flower into seed
scrp 2 7 36450 9
	setv va00 posx
	setv va01 posb
* Create seed
	new: simp 2 3 36450 "ols_jungletree" 1 2 rand 400 600
	attr 194
	bhvr 48
	tick 1500
	perm 100
	accg 1.2
	elas 30
	fric 60
	aero 1.1
	emit 7 1
	mvsf va00 va01
	targ ownr
	kill ownr
endm

* Seed timer and eat script
scrp 2 3 36450 9
	kill ownr
endm
scrp 2 3 36450 12
	stim writ from 77 1.2
	kill ownr
endm

**
** Removal script
**

*rscr
*enum 2 3 36450 kill targ next
*enum 2 4 36450 kill targ next
*enum 2 7 36450 kill targ next
*scrx 2 3 36450 9
*scrx 2 3 36450 12
*scrx 2 4 36450 1
*scrx 2 4 36450 9
*scrx 2 7 36450 9