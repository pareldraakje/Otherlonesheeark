*
**
** Ice Bush script
** Open source, do whatever you want with the code/images. No need to credit.
** Pilla
**
*
*
* CAOS2PRAY CHUNK
*# Pray-File "ols_icebush.agents"
*# DS-Name "OLS Ice Bush"
*# attach ols_icebush.c16
*# desc = "An icy bush that spawns pretty frozen stars."
*# Agent Animation File = "ols_icebush.c16"
*# Agent Animation String = "0 255"
*# Agent Sprite First Image = 0
*# Agent Animation Gallery = "ols_icebush"
*
*
* 36430 = Custom species number
* Objects:
* 2 4 36430 - Tree
* 2 7 36430 - Flower
* 2 11 36430 - Food
* Scripts:
* 2 4 36430 1 - Tree dispenser
* 2 4 36430 9 - Tree timer
* 2 7 36430 9 - Flower timer
* 2 11 36430 9 - Food timer
* 2 11 36430 12 - Food eat
*
*

* Create the tree (attr: activateable 4 + invisible 16 + collisions 64 + physics 128 = 212)
new: simp 2 4 36430 "ols_icebush" 1 0 rand 250 400
  attr 196 bhvr 0 perm 100 accg 30 elas 0 fric 60 setv ov00 0 setv ov01 0
  mvsf 33150 12770 tick 300

** The timer script
scrp 2 4 36430 9
  tick rand 800 900 setv va00 posl setv va01 post
  * Create a flower (attr: invisible 16)
  new: simp 2 7 36430 "ols_icebush" 1 1 rand 410 420 attr 16 bhvr 0 tick 2100	
  addv va00 rand 36 153 addv va01 rand 24 87 mvsf va00 va01
endm

* the tree dispenser script
scrp 2 4 36430 1
setv va00 posl setv va01 post
* Create food (attr: mouseable 2 + collisions 64 + physics 128 = 194, bhvr: eat 16 + pickup 32 = 48)
  new: simp 2 11 36430 "ols_icebush" 1 2 rand 400 600
  attr 194 bhvr 48 perm 100 accg 1.2 elas 30 fric 60 aero 1.1 emit 8 0.6
  addv va00 rand 36 153 addv va01 rand 24 87 mvsf va00 va01 tick 1600
endm

* flower script: turn flower into food
scrp 2 7 36430 9
  setv va00 posx setv va01 posb
  * Create food
  new: simp 2 11 36430 "ols_icebush" 1 2 rand 400 600 attr 194 bhvr 48 tick 1600
  perm 100 accg 1.2 elas 30 fric 60 aero 1.1 emit 8 0.4 mvsf va00 va01
  targ ownr
  kill ownr
endm

* Food timer and eat script
scrp 2 11 36430 9
  kill ownr
endm
scrp 2 11 36430 12
  stim writ from 79 1.2 kill ownr
endm

**
** Removal script
**

*rscr
*enum 2 11 36430 kill targ next
*enum 2 7 36430 kill targ next
*enum 2 4 36430 kill targ next
*scrx 2 11 36430 9
*scrx 2 11 36430 12
*scrx 2 4 36430 1
*scrx 2 4 36430 9
*scrx 2 7 36430 9