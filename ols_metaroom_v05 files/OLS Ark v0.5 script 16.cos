**
** The mushrooms in the lower corridor
** Open source, do whatever you want with the code/images. No need to credit.
** Pilla
**

*
* CAOS2PRAY CHUNK
*# Pray-File "ols_mushrooms.agents"
*# DS-Name "OLS Mushrooms"
*# attach ols_mushrooms.c16
*# desc = "Yay. Yummy mushrooms."
*# Agent Animation File = "ols_mushrooms.c16"
*# Agent Sprite First Image = 0
*# Agent Animation Gallery = "ols_mushrooms"
*

* 36443 = identifier
* Objects:
* 1 1 36443 - Seeder
* 2 11 36443 - Mushrooms
* 2 3 36433 - Mushroom seed
* Scripts:
* 1 1 36443 9 - Seeder timer
* 2 11 36443 9 - Mushroom timer
* 2 11 36443 12 - Mushroom eat
* 2 3 36443 9 - Seed timer
* 2 3 36443 12 - Seed eat

* Mushroom: 0 (seed), 1 2 3 (mushrooms)

** Mushroom seeder (attr: invisible 16)
new: simp 1 1 36443 "blnk" 1 0 1 attr 16 bhvr 0 rnge 700 mvsf 32054 13076 tick 1
** Scripts
* Mushroom seed dispenser timer
scrp 1 1 36443 9
  tick 800 setv va00 0
  esee 2 11 36443 addv va00 1 next
  doif va00 lt 4 setv va01 posx setv va02 posy
    reps 8
    * Spawn invisible mushroom seed (attr: mouseable 2 + invisible 16 + collide 64 + physics 128 = 212)
    new: simp 2 3 36443 "ols_mushrooms" 1 0 rand 1500 2000 attr 212 bhvr 0 mvsf va01 va02 
    elas 50 aero 2 perm 100 accg 0.05 velo rand -6 6 rand -8 -4 tick 40 setv ov00 150
    wait 10
    repe
  endi
endm

* Seed timer
scrp 2 3 36443 9
  doif carr eq null doif fall eq 0
    subv ov00 tick
  endi endi

  doif ov00 lt 40 gsub die
  elif ov00 lt 500 
    inst
    gsub grow
  endi
  
  subr die 
    kill ownr
  retn

  subr grow
  doif carr eq null doif fall eq 0
    setv va00 0 setv va01 0
    rnge 20 esee 2 11 36443 addv va00 1 next
    rnge 180 esee 2 11 36443 addv va01 1 next
    doif va00 eq 0
      doif va01 lt 4
        setv va02 grap posx posy setv va03 rtyp va02
        doif va03 eq 7 setv va01 posx setv va02 post
          * Create mushroom (attr: carry 2 + collide 64 + physics 128 = 194, bhvr: eat 16 + pickup 32 = 48)
          new: simp 2 11 36443 "ols_mushrooms" 1 rand 1 3 rand 1500 2000 attr 194 bhvr 48 
          perm 100 accg 1.2 elas 30 fric 60 aero 1.1 emit 8 0.8 tick rand 2000 3900
          mira rand 0 1 mvsf va01 va02
          gsub die
        endi
      endi
    endi
  endi endi
  retn
endm

* Seed eat 
scrp 2 3 36443 12
  stim writ from 77 0.9 kill ownr
endm

* Mushroom timer
scrp 2 11 36443 9
  inst
  setv va01 posx setv va02 post
  reps 3
    * Spawn mushroom seed (attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr: eat 16 + pickup 32 = 48)
    new: simp 2 3 36443 "ols_mushrooms" 1 0 rand 1500 2000 attr 194 bhvr 48 mvsf va01 va02 
    elas 50 aero 2 perm 100 accg 0.08 emit 7 0.8 velo rand -4 4 rand -6 -2 tick 40 setv ov00 rand 700 1600
  repe
  kill ownr
endm

* Mushroom eat
scrp 2 11 36443 12
  stim writ from 79 1.3 kill ownr
  reps 2
    * Spawn mushroom seed (attr: mouseable 2 + collide 64 + physics 128 = 194, bhvr: eat 16 + pickup 32 = 48)
    new: simp 2 3 36443 "ols_mushrooms" 0 1 rand 1500 2000 attr 194 bhvr 48 mvsf va01 va02 
    elas 50 aero 2 perm 100 accg 0.08 emit 7 0.8 velo rand -8 8 rand -8 -4 tick 40 setv ov00 rand 700 1600
  repe
endm

**
** Removal script
**

*rscr
*enum 1 1 36443 kill targ next
*enum 2 3 36443 kill targ next
*enum 2 11 36443 kill targ next
*scrx 1 1 36443 9
*scrx 2 3 36443 9
*scrx 2 3 36443 12
*scrx 2 11 36443 9
*scrx 2 11 36443 12